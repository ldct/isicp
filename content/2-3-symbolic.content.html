{{title}}
<title> iSICP 2.3 - Symbolic Data </title>
@@

{{main_text}}
<h2> Symbolic Data </h2> 

All the compound data objects we have used so far were constructed ultimately
from numbers.  In this section we extend the representational capability of our
language by introducing the ability to work with arbitrary symbols as data.

@menu
* 2-3-1::            Quotation
* 2-3-2::            Example: Symbolic Differentiation
* 2-3-3::            Example: Representing Sets
* 2-3-4::            Example: Huffman Encoding Trees
@end menu

<h3> Quotation </h3> 

If we can form compound data using symbols, we can have lists such as

<div id="">
(a b c d)
(23 45 17)
((Norah 12) (Molly 9) (Anna 7) (Lauren 6) (Charlotte 4))
</div>
<script>
prompt();
</script>

Lists containing symbols can look just like the expressions of our
language:

<div id="">
(* (+ 23 45) (+ x 9))

(define (fact n) (if (= n 1) 1 (* n (fact (- n 1)))))
</div>
<script>
prompt();
</script>

In order to manipulate symbols we need a new element in our language: the
ability to <tt> quote </tt> 
 a data object.  Suppose we want to construct the
list <tt> (a b) </tt>.  We can't accomplish this with <tt> (list a b) </tt>, because
this expression constructs a list of the <tt> values </tt> 
 of <tt> a </tt> and
<tt> b </tt> rather than the symbols themselves.  This issue is well known in the
context of natural languages, where words and sentences may be regarded either
as semantic entities or as character strings (syntactic entities).  The common
practice in natural languages is to use quotation marks to indicate that a word
or a sentence is to be treated literally as a string of characters.  For
instance, the first letter of ``John'' is clearly ``J.''  If we tell somebody
``say your name aloud,'' we expect to hear that person's name.  However, if we
tell somebody ``say `your name' aloud,'' we expect to hear the words ``your
name.''  Note that we are forced to nest quotation marks to describe what
somebody else might say.@footnote{Allowing quotation in a language wreaks havoc
with the ability to reason about the language in simple terms, because it
destroys the notion that equals can be substituted for equals.  For example,
three is one plus two, but the word ``three'' is not the phrase ``one plus
two.''  Quotation is powerful because it gives us a way to build expressions
that manipulate other expressions (as we will see when we write an interpreter
in Chapter 4). But allowing statements in a language that talk about
other statements in that language makes it very difficult to maintain any
coherent principle of what ``equals can be substituted for equals'' should
mean.  For example, if we know that the evening star is the morning star, then
from the statement ``the evening star is Venus'' we can deduce ``the morning
star is Venus.''  However, given that ``John knows that the evening star is
Venus'' we cannot infer that ``John knows that the morning star is Venus.''}

We can follow this same practice to identify lists and symbols that are to be
treated as data objects rather than as expressions to be evaluated.  However,
our format for quoting differs from that of natural languages in that we place
a quotation mark (traditionally, the single quote symbol <tt> ' </tt>) only at the
beginning of the object to be quoted.  We can get away with this in Scheme
syntax because we rely on blanks and parentheses to delimit objects.  Thus, the
meaning of the single quote character is to quote the next object.@footnote{The
single quote is different from the double quote we have been using to enclose
character strings to be printed.  Whereas the single quote can be used to
denote lists or symbols, the double quote is used only with character strings.
In this book, the only use for character strings is as items to be printed.}

Now we can distinguish between symbols and their values:

<div id="">
(define a 1)

(define b 2)

(list a b)
(1 2)

(list 'a 'b)
(a b)

(list 'a b)
(a 2)
</div>
<script>
prompt();
</script>

Quotation also allows us to type in compound objects, using the conventional
printed representation for lists:@footnote{Strictly, our use of the quotation
mark violates the general rule that all compound expressions in our language
should be delimited by parentheses and look like lists.  We can recover this
consistency by introducing a special form <tt> quote </tt>, which serves the same
purpose as the quotation mark.  Thus, we would type <tt> (quote a) </tt> instead of
<tt> 'a </tt>, and we would type <tt> (quote (a b c)) </tt> instead of <tt> '(a b c) </tt>.
This is precisely how the interpreter works.  The quotation mark is just a
single-character abbreviation for wrapping the next complete expression with
<tt> quote </tt> to form <tt> (quote <expression </tt>>).  This is
important because it maintains the principle that any expression seen by the
interpreter can be manipulated as a data object.  For instance, we could
construct the expression <tt> (car '(a b c)) </tt>, which is the same as <tt> (car
(quote (a b c))) </tt>, by evaluating <tt> (list 'car (list 'quote '(a b c))) </tt>.}

<div id="">
(car '(a b c))
a

(cdr '(a b c))
(b c)
</div>
<script>
prompt();
</script>

In keeping with this, we can obtain the empty list by evaluating <tt> '() </tt>,
and thus dispense with the variable <tt> nil </tt>.

One additional primitive used in manipulating symbols is <tt> eq? </tt>, which
takes two symbols as arguments and tests whether they are the same.@footnote{We
can consider two symbols to be ``the same'' if they consist of the same
characters in the same order.  Such a definition skirts a deep issue that we
are not yet ready to address: the meaning of ``sameness'' in a programming
language.  We will return to this in Chapter 3 (section 3-1-3).}
Using <tt> eq? </tt>, we can implement a useful procedure called <tt> memq </tt>.  This
takes two arguments, a symbol and a list.  If the symbol is not contained in
the list (i.e., is not <tt> eq? </tt> to any item in the list), then <tt> memq </tt>
returns false.  Otherwise, it returns the sublist of the list beginning with
the first occurrence of the symbol:

<div id="">
(define (memq item x)
  (cond ((null? x) false)
        ((eq? item (car x)) x)
        (else (memq item (cdr x)))))
</div>
<script>
prompt();
</script>

For example, the value of

<div id="">
(memq 'apple '(pear banana prune))
</div>
<script>
prompt();
</script>


is false, whereas the value of

<div id="">
(memq 'apple '(x (apple sauce) y apple pear))
</div>
<script>
prompt();
</script>


is <tt> (apple pear) </tt>.

<div class="exercise">
<b>Exercise 2.53:</b> What would the interpreter print
in response to evaluating each of the following expressions?

<div id="">
(list 'a 'b 'c)

(list (list 'george))

(cdr '((x1 x2) (y1 y2)))

(cadr '((x1 x2) (y1 y2)))

(pair? (car '(a short list)))

(memq 'red '((red shoes) (blue socks)))

(memq 'red '(red shoes blue socks))
</div>
<script>
prompt();
</script>
</div>

<div class="exercise">
<b>Exercise 2.54:</b> Two lists are said to be
<tt> equal? </tt> if they contain equal elements arranged in the same order.  For
example,

<div id="">
(equal? '(this is a list) '(this is a list))
</div>
<script>
prompt();
</script>


is true, but

<div id="">
(equal? '(this is a list) '(this (is a) list))
</div>
<script>
prompt();
</script>


is false.  To be more precise, we can define <tt> equal? </tt>  recursively in
terms of the basic <tt> eq? </tt> equality of symbols by saying that <tt> a </tt> and
<tt> b </tt> are <tt> equal? </tt> if they are both symbols and the symbols are
<tt> eq? </tt>, or if they are both lists such that <tt> (car a) </tt> is <tt> equal? </tt>
to <tt> (car b) </tt> and <tt> (cdr a) </tt> is <tt> equal? </tt> to <tt> (cdr b) </tt>.  Using
this idea, implement <tt> equal? </tt> as a procedure.@footnote{In practice,
programmers use <tt> equal? </tt> to compare lists that contain numbers as well as
symbols.  Numbers are not considered to be symbols.  The question of whether
two numerically equal numbers (as tested by <tt> = </tt>) are also <tt> eq? </tt> is
highly implementation-dependent.  A better definition of <tt> equal? </tt> (such as
the one that comes as a primitive in Scheme) would also stipulate that if
<tt> a </tt> and <tt> b </tt> are both numbers, then <tt> a </tt> and <tt> b </tt> are
<tt> equal? </tt> if they are numerically equal.}
</div>

<div class="exercise">
<b>Exercise 2.55:</b> Eva Lu Ator types to the
interpreter the expression

<div id="">
(car ''abracadabra)
</div>
<script>
prompt();
</script>

To her surprise, the interpreter prints back <tt> quote </tt>.  Explain.
</div>

<h3> Example: Symbolic Differentiation </h3> 

As an illustration of symbol manipulation and a further illustration of data
abstraction, consider the design of a procedure that performs symbolic
differentiation of algebraic expressions.  We would like the procedure to take
as arguments an algebraic expression and a variable and to return the
derivative of the expression with respect to the variable.  For example, if the
arguments to the procedure are ax^2 + bx + c and x, the
procedure should return 2ax + b.  Symbolic differentiation is of
special historical significance in Lisp.  It was one of the motivating examples
behind the development of a computer language for symbol manipulation.
Furthermore, it marked the beginning of the line of research that led to the
development of powerful systems for symbolic mathematical work, which are
currently being used by a growing number of applied mathematicians and
physicists.

In developing the symbolic-differentiation program, we will follow the same
strategy of data abstraction that we followed in developing the rational-number
system of section 2-1-1.  That is, we will first define a differentiation
algorithm that operates on abstract objects such as ``sums,'' ``products,'' and
``variables'' without worrying about how these are to be represented.  Only
afterward will we address the representation problem.

<h4> The differentiation program with abstract data </h4> 

In order to keep things simple, we will consider a very simple
symbolic-differentiation program that handles expressions that are built up
using only the operations of addition and multiplication with two arguments.
Differentiation of any such expression can be carried out by applying the
following reduction rules:

<pre>
dc
-- = 0  for c a constant, or a variable different from x
dx

dx
-- = 1
dx

d(u + v)   du   dv
-------- = -- + --
   dx      dx   dx

d(uv)     / dv \     / du \
----- = u | -- | + v | -- |
 dx       \ dx /     \ dx /
</pre>

Observe that the latter two rules are recursive in nature.  That is, to obtain
the derivative of a sum we first find the derivatives of the terms and add
them.  Each of the terms may in turn be an expression that needs to be
decomposed.  Decomposing into smaller and smaller pieces will eventually
produce pieces that are either constants or variables, whose derivatives will
be either 0 or 1.

To embody these rules in a procedure we indulge in a little wishful thinking,
as we did in designing the rational-number implementation.  If we had a means
for representing algebraic expressions, we should be able to tell whether an
expression is a sum, a product, a constant, or a variable.  We should be able
to extract the parts of an expression.  For a sum, for example we want to be
able to extract the addend (first term) and the augend (second term).  We
should also be able to construct expressions from parts.  Let us assume that we
already have procedures to implement the following selectors, constructors, and
predicates:

<pre>
(variable? e)          Is <tt> e </tt> a variable?
(same-variable? v1 v2) Are <tt> v1 </tt> and <tt> v2 </tt> the same variable?
(sum? e)               Is <tt> e </tt> a sum?
(addend e)             Addend of the sum <tt> e </tt>.
(augend e)             Augend of the sum <tt> e </tt>.
(make-sum a1 a2)       Construct the sum of <tt> a1 </tt> and <tt> a2 </tt>.
(product? e)           Is <tt> e </tt> a product?
(multiplier e)         Multiplier of the product <tt> e </tt>.
(multiplicand e)       Multiplicand of the product <tt> e </tt>.
(make-product m1 m2)   Construct the product of <tt> m1 </tt> and <tt> m2 </tt>.
</pre>

Using these, and the primitive predicate <tt> number? </tt>, which identifies
numbers, we can express the differentiation rules as the following procedure:

<div id="">
(define (deriv exp var)
  (cond ((number? exp) 0)
        ((variable? exp)
         (if (same-variable? exp var) 1 0))
        ((sum? exp)
         (make-sum (deriv (addend exp) var)
                   (deriv (augend exp) var)))
        ((product? exp)
         (make-sum
           (make-product (multiplier exp)
                         (deriv (multiplicand exp) var))
           (make-product (deriv (multiplier exp) var)
                         (multiplicand exp))))
        (else
         (error "unknown expression type -- DERIV" exp))))
</div>
<script>
prompt();
</script>

This <tt> deriv </tt> procedure incorporates the complete differentiation
algorithm.  Since it is expressed in terms of abstract data, it will work no
matter how we choose to represent algebraic expressions, as long as we design a
proper set of selectors and constructors.  This is the issue we must address
next.

<h4> Representing algebraic expressions </h4> 

We can imagine many ways to use list structure to represent algebraic
expressions.  For example, we could use lists of symbols that mirror the usual
algebraic notation, representing ax + b as the list <tt> (a * x +
b) </tt> .  However, one especially straightforward choice is to use the same
parenthesized prefix notation that Lisp uses for combinations; that is, to
represent ax + b as <tt> (+ (* a x) b) </tt>.  Then our data
representation for the differentiation problem is as follows:

<ul>

@item
The variables are symbols.  They are identified by the primitive predicate
<tt> symbol? </tt>:

<div id="">
(define (variable? x) (symbol? x))
</div>
<script>
prompt();
</script>

@item
Two variables are the same if the symbols representing them are <tt> eq? </tt>:

<div id="">
(define (same-variable? v1 v2)
  (and (variable? v1) (variable? v2) (eq? v1 v2)))
</div>
<script>
prompt();
</script>

@item
Sums and products are constructed as lists:

<div id="">
(define (make-sum a1 a2) (list '+ a1 a2))

(define (make-product m1 m2) (list '* m1 m2))
</div>
<script>
prompt();
</script>

@item
A sum is a list whose first element is the symbol <tt> + </tt>:

<div id="">
(define (sum? x)
  (and (pair? x) (eq? (car x) '+)))
</div>
<script>
prompt();
</script>

@item
The addend is the second item of the sum list:

<div id="">
(define (addend s) (cadr s))
</div>
<script>
prompt();
</script>

@item
The augend is the third item of the sum list:

<div id="">
(define (augend s) (caddr s))
</div>
<script>
prompt();
</script>

@item
A product is a list whose first element is the symbol <tt> * </tt>:

<div id="">
(define (product? x)
  (and (pair? x) (eq? (car x) '*)))
</div>
<script>
prompt();
</script>

@item
The multiplier is the second item of the product list:

<div id="">
(define (multiplier p) (cadr p))
</div>
<script>
prompt();
</script>

@item
The multiplicand is the third item of the product list:

<div id="">
(define (multiplicand p) (caddr p))
</div>
<script>
prompt();
</script>

</ul>

Thus, we need only combine these with the algorithm as embodied by <tt> deriv </tt>
in order to have a working symbolic-differentiation program.  Let us look at
some examples of its behavior:

<div id="">
(deriv '(+ x 3) 'x)
(+ 1 0)

(deriv '(* x y) 'x)
(+ (* x 0) (* 1 y))

(deriv '(* (* x y) (+ x 3)) 'x)
   (+ (* (* x y) (+ 1 0))
   (* (+ (* x 0) (* 1 y))
      (+  x 3)))
</div>
<script>
prompt();
</script>

The program produces answers that are correct; however, they are unsimplified.
It is true that

<pre>
d(xy)
----- = x * 0 + 1 * y
 dx
</pre>


but we would like the program to know that x * 0 = 0, 1 * y = y,
and 0 + y = y.  The answer for the second example should have been
simply <tt> y </tt>.  As the third example shows, this becomes a serious issue when
the expressions are complex.

Our difficulty is much like the one we encountered with the rational-number
implementation: we haven't reduced answers to simplest form.  To accomplish the
rational-number reduction, we needed to change only the constructors and the
selectors of the implementation.  We can adopt a similar strategy here.  We
won't change <tt> deriv </tt> at all.  Instead, we will change <tt> make-sum </tt> so
that if both summands are numbers, <tt> make-sum </tt> will add them and return
their sum.  Also, if one of the summands is 0, then <tt> make-sum </tt> will return
the other summand.

<div id="">
(define (make-sum a1 a2)
  (cond ((=number? a1 0) a2)
        ((=number? a2 0) a1)
        ((and (number? a1) (number? a2)) (+ a1 a2))
        (else (list '+ a1 a2))))
</div>
<script>
prompt();
</script>

This uses the procedure <tt> =number? </tt>, which checks whether an expression is
equal to a given number:

<div id="">
(define (=number? exp num)
  (and (number? exp) (= exp num)))
</div>
<script>
prompt();
</script>

Similarly, we will change <tt> make-product </tt> to build in the rules that 0
times anything is 0 and 1 times anything is the thing itself:

<div id="">
(define (make-product m1 m2)
  (cond ((or (=number? m1 0) (=number? m2 0)) 0)
        ((=number? m1 1) m2)
        ((=number? m2 1) m1)
        ((and (number? m1) (number? m2)) (* m1 m2))
        (else (list '* m1 m2))))
</div>
<script>
prompt();
</script>

Here is how this version works on our three examples:

<div id="">
(deriv '(+ x 3) 'x)
1

(deriv '(* x y) 'x)
y

(deriv '(* (* x y) (+ x 3)) 'x)
(+ (* x y) (* y (+ x 3)))
</div>
<script>
prompt();
</script>

Although this is quite an improvement, the third example shows that there is
still a long way to go before we get a program that puts expressions into a
form that we might agree is ``simplest.''  The problem of algebraic
simplification is complex because, among other reasons, a form that may be
simplest for one purpose may not be for another.

<div class="exercise">
<b>Exercise 2.56:</b> Show how to extend the basic
differentiator to handle more kinds of expressions.  For instance, implement
the differentiation rule

<pre>
n_1   n_2
--- = ---  if and only if n_1 d_2 = n_2 d_1
d_1   d_2
</pre>


by adding a new clause to the <tt> deriv </tt> program and defining appropriate
procedures <tt> exponentiation? </tt>, <tt> base </tt>, <tt> exponent </tt>, and
<tt> make-exponentiation </tt>.  (You may use the symbol <tt> ** </tt> to denote
exponentiation.)  Build in the rules that anything raised to the power 0 is 1
and anything raised to the power 1 is the thing itself.
</div>

<div class="exercise">
<b>Exercise 2.57:</b> Extend the differentiation
program to handle sums and products of arbitrary numbers of (two or more)
terms.  Then the last example above could be expressed as

<div id="">
(deriv '(* x y (+ x 3)) 'x)
</div>
<script>
prompt();
</script>

Try to do this by changing only the representation for sums and products,
without changing the <tt> deriv </tt> procedure at all.  For example, the
<tt> addend </tt> of a sum would be the first term, and the <tt> augend </tt> would be
the sum of the rest of the terms.
</div>

<div class="exercise">
<b>Exercise 2.58:</b> Suppose we want to modify the
differentiation program so that it works with ordinary mathematical notation,
in which <tt> + </tt> and <tt> * </tt> are infix rather than prefix operators.  Since
the differentiation program is defined in terms of abstract data, we can modify
it to work with different representations of expressions solely by changing the
predicates, selectors, and constructors that define the representation of the
algebraic expressions on which the differentiator is to operate.

<ul>
@item
Show how to do this in order to differentiate algebraic expressions presented
in infix form, such as <tt> (x + (3 * (x + (y + 2)))) </tt>.  To simplify the task,
assume that <tt> + </tt> and <tt> * </tt> always take two arguments and that
expressions are fully parenthesized.

@item
The problem becomes substantially harder if we allow standard algebraic
notation, such as <tt> (x + 3 * (x + y + 2)) </tt>, which drops unnecessary
parentheses and assumes that multiplication is done before addition.  Can you
design appropriate predicates, selectors, and constructors for this notation
such that our derivative program still works?

</ul>
</div>

<h3> Example: Representing Sets </h3> 

In the previous examples we built representations for two kinds of compound
data objects: rational numbers and algebraic expressions.  In one of these
examples we had the choice of simplifying (reducing) the expressions at either
construction time or selection time, but other than that the choice of a
representation for these structures in terms of lists was straightforward. When
we turn to the representation of sets, the choice of a representation is not so
obvious.  Indeed, there are a number of possible representations, and they
differ significantly from one another in several ways.

Informally, a set is simply a collection of distinct objects.  To give a more
precise definition we can employ the method of data abstraction.  That is, we
define ``set'' by specifying the operations that are to be used on sets.  These
are <tt> union-set </tt>, <tt> intersection-set </tt>, <tt> element-of-set? </tt>, and
<tt> adjoin-set </tt>.  <tt> Element-of-set? </tt> is a predicate that determines
whether a given element is a member of a set.  <tt> Adjoin-set </tt> takes an
object and a set as arguments and returns a set that contains the elements of
the original set and also the adjoined element.  <tt> Union-set </tt> computes the
union of two sets, which is the set containing each element that appears in
either argument.  <tt> Intersection-set </tt> computes the intersection of two
sets, which is the set containing only elements that appear in both arguments.
From the viewpoint of data abstraction, we are free to design any
representation that implements these operations in a way consistent with the
interpretations given above.@footnote{If we want to be more formal, we can
specify ``consistent with the interpretations given above'' to mean that the
operations satisfy a collection of rules such as these:

<ul>

@item
For any set <tt> S </tt> and any object <tt> x </tt>,
<tt> (element-of-set? x (adjoin-set x S)) </tt>
is true (informally: ``Adjoining an object to a
set produces a set that contains the object'').

@item
For any sets <tt> S </tt> and <tt> T </tt> and any object <tt> x </tt>,
<tt> (element-of-set? x (union-set S T)) </tt>
is equal to
<tt> (or (element-of-set? x S) (element-of-set? x T)) </tt>
(informally: ``The elements of <tt> (union S T) </tt> are the elements that
are in <tt> S </tt> or in <tt> T </tt>'').

@item
For any object <tt> x </tt>,
<tt> (element-of-set? x '()) </tt>
is false (informally: ``No object is an element of the empty set'').

</ul>
}

<h4> Sets as unordered lists </h4> 

One way to represent a set is as a list of its elements in which no element
appears more than once.  The empty set is represented by the empty list.  In
this representation, <tt> element-of-set? </tt> is similar to the procedure
<tt> memq </tt> of section 2-3-1.  It uses <tt> equal? </tt>  instead of
<tt> eq? </tt> so that the set elements need not be symbols:

<div id="">
(define (element-of-set? x set)
  (cond ((null? set) false)
        ((equal? x (car set)) true)
        (else (element-of-set? x (cdr set)))))
</div>
<script>
prompt();
</script>

Using this, we can write <tt> adjoin-set </tt>.  If the object to be adjoined is
already in the set, we just return the set.  Otherwise, we use <tt> cons </tt> to
add the object to the list that represents the set:

<div id="">
(define (adjoin-set x set)
  (if (element-of-set? x set)
      set
      (cons x set)))
</div>
<script>
prompt();
</script>

For <tt> intersection-set </tt> we can use a recursive strategy.  If we know how to
form the intersection of <tt> set2 </tt> and the <tt> cdr </tt> of <tt> set1 </tt>, we only
need to decide whether to include the <tt> car </tt> of <tt> set1 </tt> in this.  But
this depends on whether <tt> (car set1) </tt> is also in <tt> set2 </tt>.  Here is the
resulting procedure:

<div id="">
(define (intersection-set set1 set2)
  (cond ((or (null? set1) (null? set2)) '())
        ((element-of-set? (car set1) set2)
         (cons (car set1)
               (intersection-set (cdr set1) set2)))
        (else (intersection-set (cdr set1) set2))))
</div>
<script>
prompt();
</script>

In designing a representation, one of the issues we should be concerned with is
efficiency.  Consider the number of steps required by our set operations.
Since they all use <tt> element-of-set? </tt>, the speed of this operation has a
major impact on the efficiency of the set implementation as a whole.  Now, in
order to check whether an object is a member of a set, <tt> element-of-set? </tt>
may have to scan the entire set. (In the worst case, the object turns out not
to be in the set.)  Hence, if the set has n elements,
<tt> element-of-set? </tt>  might take up to n steps.  Thus, the number of
steps required grows as [theta](n).  The number of steps required by
<tt> adjoin-set </tt>, which uses this operation, also grows as [theta](n).
For <tt> intersection-set </tt>, which does an <tt> element-of-set? </tt> check for
each element of <tt> set1 </tt>, the number of steps required grows as the product
of the sizes of the sets involved, or [theta](n^2) for two sets of size
n.  The same will be true of <tt> union-set </tt>.

<div class="exercise">
<b>Exercise 2.59:</b> Implement the <tt> union-set </tt>
operation for the unordered-list representation of sets.
</div>

<div class="exercise">
<b>Exercise 2.60:</b> We specified that a set would be
represented as a list with no duplicates.  Now suppose we allow duplicates.
For instance, the set @{1,2,3@} could be represented as the list <tt> (2 3 2 1
3 2 2) </tt>.  Design procedures <tt> element-of-set? </tt>, <tt> adjoin-set </tt>,
<tt> union-set </tt>, and <tt> intersection-set </tt> that operate on this
representation.  How does the efficiency of each compare with the corresponding
procedure for the non-duplicate representation?  Are there applications for
which you would use this representation in preference to the non-duplicate one?
</div>

<h4> Sets as ordered lists </h4> 

One way to speed up our set operations is to change the representation so that
the set elements are listed in increasing order.  To do this, we need some way
to compare two objects so that we can say which is bigger.  For example, we
could compare symbols lexicographically, or we could agree on some method for
assigning a unique number to an object and then compare the elements by
comparing the corresponding numbers.  To keep our discussion simple, we will
consider only the case where the set elements are numbers, so that we can
compare elements using <tt> > </tt> and <tt> < </tt>.  We will represent a set of
numbers by listing its elements in increasing order.  Whereas our first
representation above allowed us to represent the set @{1,3,6,10@} by listing
the elements in any order, our new representation allows only the list <tt> (1
3 6 10) </tt>.

One advantage of ordering shows up in <tt> element-of-set? </tt>: In checking for
the presence of an item, we no longer have to scan the entire set.  If we reach
a set element that is larger than the item we are looking for, then we know
that the item is not in the set:

<div id="">
(define (element-of-set? x set)
  (cond ((null? set) false)
        ((= x (car set)) true)
        ((< x (car set)) false)
        (else (element-of-set? x (cdr set)))))
</div>
<script>
prompt();
</script>

How many steps does this save?  In the worst case, the item we are looking for
may be the largest one in the set, so the number of steps is the same as for
the unordered representation.  On the other hand, if we search for items of
many different sizes we can expect that sometimes we will be able to stop
searching at a point near the beginning of the list and that other times we
will still need to examine most of the list.  On the average we should expect
to have to examine about half of the items in the set.  Thus, the average
number of steps required will be about n/2.  This is still
[theta](n) growth, but it does save us, on the average, a factor of 2
in number of steps over the previous implementation.

We obtain a more impressive speedup with <tt> intersection-set </tt>.  In the
unordered representation this operation required [theta](n^2) steps,
because we performed a complete scan of <tt> set2 </tt> for each element of
<tt> set1 </tt>.  But with the ordered representation, we can use a more clever
method.  Begin by comparing the initial elements, <tt> x1 </tt> and <tt> x2 </tt>, of
the two sets.  If <tt> x1 </tt> equals <tt> x2 </tt>, then that gives an element of the
intersection, and the rest of the intersection is the intersection of the
<tt> cdr </tt>s of the two sets.  Suppose, however, that <tt> x1 </tt> is less than
<tt> x2 </tt>.  Since <tt> x2 </tt> is the smallest element in <tt> set2 </tt>, we can
immediately conclude that <tt> x1 </tt> cannot appear anywhere in <tt> set2 </tt> and
hence is not in the intersection.  Hence, the intersection is equal to the
intersection of <tt> set2 </tt> with the <tt> cdr </tt> of <tt> set1 </tt>.  Similarly, if
<tt> x2 </tt> is less than <tt> x1 </tt>, then the intersection is given by the
intersection of <tt> set1 </tt> with the <tt> cdr </tt> of <tt> set2 </tt>.  Here is the
procedure:

<div id="">
(define (intersection-set set1 set2)
  (if (or (null? set1) (null? set2))
      '()
      (let ((x1 (car set1)) (x2 (car set2)))
        (cond ((= x1 x2)
               (cons x1
                     (intersection-set (cdr set1)
                                       (cdr set2))))
              ((< x1 x2)
               (intersection-set (cdr set1) set2))
              ((< x2 x1)
               (intersection-set set1 (cdr set2)))))))
</div>
<script>
prompt();
</script>

To estimate the number of steps required by this process, observe that at each
step we reduce the intersection problem to computing intersections of smaller
sets---removing the first element from <tt> set1 </tt> or <tt> set2 </tt> or both.
Thus, the number of steps required is at most the sum of the sizes of
<tt> set1 </tt> and <tt> set2 </tt>, rather than the product of the sizes as with the
unordered representation.  This is [theta](n) growth rather than
[theta](n^2)---a considerable speedup, even for sets of moderate size.

<div class="exercise">
<b>Exercise 2.61:</b> Give an implementation of
<tt> adjoin-set </tt> using the ordered representation.  By analogy with
<tt> element-of-set? </tt> show how to take advantage of the ordering to produce a
procedure that requires on the average about half as many steps as with the
unordered representation.
</div>

<div class="exercise">
<b>Exercise 2.62:</b> Give a [theta](n)
implementation of <tt> union-set </tt> for sets represented as ordered lists.
</div>

<h4> Sets as binary trees </h4> 

We can do better than the ordered-list representation by arranging the set
elements in the form of a tree.  Each node of the tree holds one element of the
set, called the ``entry'' at that node, and a link to each of two other
(possibly empty) nodes.  The ``left'' link points to elements smaller than the
one at the node, and the ``right'' link to elements greater than the one at the
node.  Figure 2-16 shows some trees that represent the set
@{1,3,5,7,9,11@}.  The same set may be represented by a tree in a number of
different ways.  The only thing we require for a valid representation is that
all elements in the left subtree be smaller than the node entry and that all
elements in the right subtree be larger.

<div class="exercise">
<b>Figure 2.16:</b> Various binary trees that represent
the set @{1,3,5,7,9,11@}.

<pre>
   7          3             5
   /\         /\            /\
  3  9       1  7          3  9
 /\   \         /\        /   /\
1  5  11       5  9      1   7  11
                   \
                   11
</pre>
</div>

The advantage of the tree representation is this: Suppose we want to check
whether a number x is contained in a set.  We begin by comparing x with
the entry in the top node.  If x is less than this, we know that we need
only search the left subtree; if x is greater, we need only search the
right subtree.  Now, if the tree is ``balanced,'' each of these subtrees will
be about half the size of the original.  Thus, in one step we have reduced the
problem of searching a tree of size n to searching a tree of size n/2.
Since the size of the tree is halved at each step, we should expect that the
number of steps needed to search a tree of size n grows as
[theta](<tt> log </tt> n).@footnote{Halving the size of the problem at each
step is the distinguishing characteristic of logarithmic growth, as we saw with
the fast-exponentiation algorithm of section 1-2-4 and the half-interval
search method of section 1-3-3.} For large sets, this will be a
significant speedup over the previous representations.

We can represent trees by using lists.  Each node will be a list of three
items: the entry at the node, the left subtree, and the right subtree.  A left
or a right subtree of the empty list will indicate that there is no subtree
connected there.  We can describe this representation by the following
procedures:@footnote{We are representing sets in terms of trees, and trees in
terms of lists---in effect, a data abstraction built upon a data abstraction.
We can regard the procedures <tt> entry </tt>, <tt> left-branch </tt>,
<tt> right-branch </tt>, and <tt> make-tree </tt> as a way of isolating the abstraction
of a ``binary tree'' from the particular way we might wish to represent such a
tree in terms of list structure.}

<div id="">
(define (entry tree) (car tree))

(define (left-branch tree) (cadr tree))

(define (right-branch tree) (caddr tree))

(define (make-tree entry left right)
  (list entry left right))
</div>
<script>
prompt();
</script>

Now we can write the <tt> element-of-set? </tt> procedure using the strategy
described above:

<div id="">
(define (element-of-set? x set)
  (cond ((null? set) false)
        ((= x (entry set)) true)
        ((< x (entry set))
         (element-of-set? x (left-branch set)))
        ((> x (entry set))
         (element-of-set? x (right-branch set)))))
</div>
<script>
prompt();
</script>

Adjoining an item to a set is implemented similarly and also requires
[theta](<tt> log </tt> n) steps.  To adjoin an item <tt> x </tt>, we compare
<tt> x </tt> with the node entry to determine whether <tt> x </tt> should be added to
the right or to the left branch, and having adjoined <tt> x </tt> to the
appropriate branch we piece this newly constructed branch together with the
original entry and the other branch.  If <tt> x </tt> is equal to the entry, we
just return the node.  If we are asked to adjoin <tt> x </tt> to an empty tree, we
generate a tree that has <tt> x </tt> as the entry and empty right and left
branches.  Here is the procedure:

<div id="">
(define (adjoin-set x set)
  (cond ((null? set) (make-tree x '() '()))
        ((= x (entry set)) set)
        ((< x (entry set))
         (make-tree (entry set)
                    (adjoin-set x (left-branch set))
                    (right-branch set)))
        ((> x (entry set))
         (make-tree (entry set)
                    (left-branch set)
                    (adjoin-set x (right-branch set))))))
</div>
<script>
prompt();
</script>

The above claim that searching the tree can be performed in a logarithmic
number of steps rests on the assumption that the tree is ``balanced,'' i.e.,
that the left and the right subtree of every tree have approximately the same
number of elements, so that each subtree contains about half the elements of
its parent.  But how can we be certain that the trees we construct will be
balanced?  Even if we start with a balanced tree, adding elements with
<tt> adjoin-set </tt> may produce an unbalanced result.  Since the position of a
newly adjoined element depends on how the element compares with the items
already in the set, we can expect that if we add elements ``randomly'' the tree
will tend to be balanced on the average.  But this is not a guarantee.  For
example, if we start with an empty set and adjoin the numbers 1 through 7 in
sequence we end up with the highly unbalanced tree shown in Figure 2-17.
In this tree all the left subtrees are empty, so it has no advantage over a
simple ordered list.  One way to solve this problem is to define an operation
that transforms an arbitrary tree into a balanced tree with the same elements.
Then we can perform this transformation after every few <tt> adjoin-set </tt>
operations to keep our set in balance.  There are also other ways to solve this
problem, most of which involve designing new data structures for which
searching and insertion both can be done in [theta](<tt> log </tt> n)
steps.@footnote{Examples of such structures include <tt> B-trees </tt> 
 and
<tt> red-black trees </tt> 
.  There is a large literature on data structures
devoted to this problem.  See Cormen, Leiserson, and Rivest 1990.}

<div class="exercise">
<b>Figure 2.17:</b> Unbalanced tree produced by adjoining
1 through 7 in sequence.

<pre>
1
 \
  2
   \
    4
     \
      5
       \
        6
         \
          7
</pre>
</div>

<div class="exercise">
<b>Exercise 2.63:</b> Each of the following two
procedures converts a binary tree to a list.

<div id="">
(define (tree->list-1 tree)
  (if (null? tree)
      '()
      (append (tree->list-1 (left-branch tree))
              (cons (entry tree)
                    (tree->list-1 (right-branch tree))))))

(define (tree->list-2 tree)
  (define (copy-to-list tree result-list)
    (if (null? tree)
        result-list
        (copy-to-list (left-branch tree)
                      (cons (entry tree)
                            (copy-to-list (right-branch tree)
                                          result-list)))))
  (copy-to-list tree '()))
</div>
<script>
prompt();
</script>

<ul>
@item
Do the two procedures produce the same result for every tree?  If not, how do
the results differ?  What lists do the two procedures produce for the trees in
Figure 2-16?

@item
Do the two procedures have the same order of growth in the number of steps
required to convert a balanced tree with n elements to a list?  If not,
which one grows more slowly?

</ul>
</div>

<div class="exercise">
<b>Exercise 2.64:</b> The following procedure
<tt> list->tree </tt> converts an ordered list to a balanced binary tree.  The
helper procedure <tt> partial-tree </tt> takes as arguments an integer n and
list of at least n elements and constructs a balanced tree containing the
first n elements of the list.  The result returned by <tt> partial-tree </tt>
is a pair (formed with <tt> cons </tt>) whose <tt> car </tt> is the constructed tree
and whose <tt> cdr </tt> is the list of elements not included in the tree.

<div id="">
(define (list->tree elements)
  (car (partial-tree elements (length elements))))

(define (partial-tree elts n)
  (if (= n 0)
      (cons '() elts)
      (let ((left-size (quotient (- n 1) 2)))
        (let ((left-result (partial-tree elts left-size)))
          (let ((left-tree (car left-result))
                (non-left-elts (cdr left-result))
                (right-size (- n (+ left-size 1))))
            (let ((this-entry (car non-left-elts))
                  (right-result (partial-tree (cdr non-left-elts)
                                              right-size)))
              (let ((right-tree (car right-result))
                    (remaining-elts (cdr right-result)))
                (cons (make-tree this-entry left-tree right-tree)
                      remaining-elts))))))))
</div>
<script>
prompt();
</script>

<ul>
@item
Write a short paragraph explaining as clearly as you can how
<tt> partial-tree </tt> works.  Draw the tree produced by <tt> list->tree </tt> for
the list <tt> (1 3 5 7 9 11) </tt>.

@item
What is the order of growth in the number of steps required by
<tt> list->tree </tt> to convert a list of n elements?

</ul>
</div>

<div class="exercise">
<b>Exercise 2.65:</b> Use the results of Exercise
2-63 and Exercise 2-64 to give [theta](n) implementations of
<tt> union-set </tt> and <tt> intersection-set </tt> for sets implemented as (balanced)
binary trees.@footnote{Exercise 2-63 through Exercise 2-65 are due
to Paul Hilfinger.}
</div>

<h4> Sets and information retrieval </h4> 

We have examined options for using lists to represent sets and have seen how
the choice of representation for a data object can have a large impact on the
performance of the programs that use the data.  Another reason for
concentrating on sets is that the techniques discussed here appear again and
again in applications involving information retrieval.

Consider a data base containing a large number of individual records, such as
the personnel files for a company or the transactions in an accounting system.
A typical data-management system spends a large amount of time accessing or
modifying the data in the records and therefore requires an efficient method
for accessing records.  This is done by identifying a part of each record to
serve as an identifying <tt> key </tt> 
.  A key can be anything that uniquely
identifies the record.  For a personnel file, it might be an employee's ID
number.  For an accounting system, it might be a transaction number.  Whatever
the key is, when we define the record as a data structure we should include a
<tt> key </tt> selector procedure that retrieves the key associated with a given
record.

Now we represent the data base as a set of records. To locate the record with a
given key we use a procedure <tt> lookup </tt>, which takes as arguments a key and
a data base and which returns the record that has that key, or false if there
is no such record.  <tt> Lookup </tt> is implemented in almost the same way as
<tt> element-of-set? </tt>.  For example, if the set of records is implemented as
an unordered list, we could use

<div id="">
(define (lookup given-key set-of-records)
  (cond ((null? set-of-records) false)
        ((equal? given-key (key (car set-of-records)))
         (car set-of-records))
        (else (lookup given-key (cdr set-of-records)))))
</div>
<script>
prompt();
</script>

Of course, there are better ways to represent large sets than as unordered
lists.  Information-retrieval systems in which records have to be ``randomly
accessed'' are typically implemented by a tree-based method, such as the
binary-tree representation discussed previously.  In designing such a system
the methodology of data abstraction can be a great help.  The designer can
create an initial implementation using a simple, straightforward representation
such as unordered lists.  This will be unsuitable for the eventual system, but
it can be useful in providing a ``quick and dirty'' data base with which to
test the rest of the system.  Later on, the data representation can be modified
to be more sophisticated.  If the data base is accessed in terms of abstract
selectors and constructors, this change in representation will not require any
changes to the rest of the system.

<div class="exercise">
<b>Exercise 2.66:</b> Implement the <tt> lookup </tt>
procedure for the case where the set of records is structured as a binary tree,
ordered by the numerical values of the keys.
</div>

<h3> Example: Huffman Encoding Trees </h3> 

This section provides practice in the use of list structure and data
abstraction to manipulate sets and trees.  The application is to methods for
representing data as sequences of ones and zeros (bits).  For example, the
ASCII standard code used to represent text in computers encodes each character
as a sequence of seven bits.  Using seven bits allows us to distinguish 2^(7),
or 128, possible different characters.  In general, if we want to distinguish
n different symbols, we will need to use <tt> log </tt>_2 n bits per
symbol.  If all our messages are made up of the eight symbols A, B, C, D, E, F,
G, and H, we can choose a code with three bits per character, for example

<pre>
A 000 C 010 E 100 G 110
B 001 D 011 F 101 H 111
</pre>


With this code, the message

BACADAEAFABBAAAGAH


is encoded as the string of 54 bits

001000010000011000100000101000001001000000000110000111

Codes such as ASCII and the A-through-H code above are known as
<tt> fixed-length </tt> 
 codes, because they represent each symbol in the message
with the same number of bits.  It is sometimes advantageous to use
<tt> variable-length </tt> 
 codes, in which different symbols may be represented
by different numbers of bits.  For example, Morse code does not use the same
number of dots and dashes for each letter of the alphabet.  In particular, E,
the most frequent letter, is represented by a single dot.  In general, if our
messages are such that some symbols appear very frequently and some very
rarely, we can encode data more efficiently (i.e., using fewer bits per
message) if we assign shorter codes to the frequent symbols.  Consider the
following alternative code for the letters A through H:

<pre>
A 0   C 1010  E 1100  G 1110
B 100 D 1011  F 1101  H 1111
</pre>


With this code, the same message as above is encoded as the string

100010100101101100011010100100000111001111

This string contains 42 bits, so it saves more than 20% in space in comparison
with the fixed-length code shown above.

One of the difficulties of using a variable-length code is knowing when you
have reached the end of a symbol in reading a sequence of zeros and ones.
Morse code solves this problem by using a special <tt> separator code </tt> 
 (in
this case, a pause) after the sequence of dots and dashes for each letter.
Another solution is to design the code in such a way that no complete code for
any symbol is the beginning (or <tt> prefix </tt> 
) of the code for another
symbol.  Such a code is called a <tt> prefix code </tt> 
.  In the example above,
A is encoded by 0 and B is encoded by 100, so no other symbol can have a code
that begins with 0 or with 100.

In general, we can attain significant savings if we use variable-length prefix
codes that take advantage of the relative frequencies of the symbols in the
messages to be encoded.  One particular scheme for doing this is called the
Huffman encoding method, after its discoverer, David Huffman.  A Huffman code
can be represented as a binary tree whose leaves are the symbols that are
encoded.  At each non-leaf node of the tree there is a set containing all the
symbols in the leaves that lie below the node.  In addition, each symbol at a
leaf is assigned a weight (which is its relative frequency), and each non-leaf
node contains a weight that is the sum of all the weights of the leaves lying
below it.  The weights are not used in the encoding or the decoding process.
We will see below how they are used to help construct the tree.

<div class="exercise">
<b>Figure 2.18:</b> A Huffman encoding tree.

<pre>
           @{A B C D E F G H@} 17
                    *
                   / \
                  /   \
                A 8    * @{B C D E F G H@} 9
            __________/ \_____________
           /                          \
@{B C D@} 5 *                            * @{E F G H@} 4
         / \                       ___/ \___
        /   \                     /         \
      B 3    * @{C D@} 2   @{E F@} 2 *           * @{G H@} 2
            / \                 / \         / \
           /   \               /   \       /   \
         C 1   D 1           E 1   F 1   G 1   H 1
</pre>
</div>

Figure 2-18 shows the Huffman tree for the A-through-H code given above.
The weights at the leaves indicate that the tree was designed for messages in
which A appears with relative frequency 8, B with relative frequency 3, and the
other letters each with relative frequency 1.

Given a Huffman tree, we can find the encoding of any symbol by starting at the
root and moving down until we reach the leaf that holds the symbol.  Each time
we move down a left branch we add a 0 to the code, and each time we move down a
right branch we add a 1.  (We decide which branch to follow by testing to see
which branch either is the leaf node for the symbol or contains the symbol in
its set.)  For example, starting from the root of the tree in Figure
2-18, we arrive at the leaf for D by following a right branch, then a left
branch, then a right branch, then a right branch; hence, the code for D is
1011.

To decode a bit sequence using a Huffman tree, we begin at the root and use the
successive zeros and ones of the bit sequence to determine whether to move down
the left or the right branch.  Each time we come to a leaf, we have generated a
new symbol in the message, at which point we start over from the root of the
tree to find the next symbol.  For example, suppose we are given the tree above
and the sequence 10001010.  Starting at the root, we move down the right
branch, (since the first bit of the string is 1), then down the left branch
(since the second bit is 0), then down the left branch (since the third bit is
also 0).  This brings us to the leaf for B, so the first symbol of the decoded
message is B.  Now we start again at the root, and we make a left move because
the next bit in the string is 0.  This brings us to the leaf for A.  Then we
start again at the root with the rest of the string 1010, so we move right,
left, right, left and reach C.  Thus, the entire message is BAC.

<h4> Generating Huffman trees </h4> 

Given an ``alphabet'' of symbols and their relative frequencies, how do we
construct the ``best'' code?  (In other words, which tree will encode messages
with the fewest bits?)  Huffman gave an algorithm for doing this and showed
that the resulting code is indeed the best variable-length code for messages
where the relative frequency of the symbols matches the frequencies with which
the code was constructed.  We will not prove this optimality of Huffman codes
here, but we will show how Huffman trees are constructed.@footnote{See Hamming
1980 for a discussion of the mathematical properties of Huffman codes.}

The algorithm for generating a Huffman tree is very simple. The idea is to
arrange the tree so that the symbols with the lowest frequency appear farthest
away from the root. Begin with the set of leaf nodes, containing symbols and
their frequencies, as determined by the initial data from which the code is to
be constructed. Now find two leaves with the lowest weights and merge them to
produce a node that has these two nodes as its left and right branches. The
weight of the new node is the sum of the two weights. Remove the two leaves
from the original set and replace them by this new node. Now continue this
process. At each step, merge two nodes with the smallest weights, removing them
from the set and replacing them with a node that has these two as its left and
right branches. The process stops when there is only one node left, which is
the root of the entire tree.  Here is how the Huffman tree of Figure 2-18
was generated:

<pre>
Initial leaves @{(A 8) (B 3) (C 1) (D 1) (E 1) (F 1) (G 1) (H 1)@}
Merge          @{(A 8) (B 3) (@{C D@} 2) (E 1) (F 1) (G 1) (H 1)@}
Merge          @{(A 8) (B 3) (@{C D@} 2) (@{E F@} 2) (G 1) (H 1)@}
Merge          @{(A 8) (B 3) (@{C D@} 2) (@{E F@} 2) (@{G H@} 2)@}
Merge          @{(A 8) (B 3) (@{C D@} 2) (@{E F G H@} 4)@}
Merge          @{(A 8) (@{B C D@} 5) (@{E F G H@} 4)@}
Merge          @{(A 8) (@{B C D E F G H@} 9)@}
Final merge    @{(@{A B C D E F G H@} 17)@}
</pre>

The algorithm does not always specify a unique tree, because there may not be
unique smallest-weight nodes at each step.  Also, the choice of the order in
which the two nodes are merged (i.e., which will be the right branch and which
will be the left branch) is arbitrary.

<h4> Representing Huffman trees </h4> 

In the exercises below we will work with a system that uses Huffman trees to
encode and decode messages and generates Huffman trees according to the
algorithm outlined above.  We will begin by discussing how trees are
represented.

Leaves of the tree are represented by a list consisting of the symbol
<tt> leaf </tt>, the symbol at the leaf, and the weight:

<div id="">
(define (make-leaf symbol weight)
  (list 'leaf symbol weight))

(define (leaf? object)
  (eq? (car object) 'leaf))

(define (symbol-leaf x) (cadr x))

(define (weight-leaf x) (caddr x))
</div>
<script>
prompt();
</script>

A general tree will be a list of a left branch, a right branch, a set of
symbols, and a weight.  The set of symbols will be simply a list of the
symbols, rather than some more sophisticated set representation.  When we make
a tree by merging two nodes, we obtain the weight of the tree as the sum of the
weights of the nodes, and the set of symbols as the union of the sets of
symbols for the nodes.  Since our symbol sets are represented as lists, we can
form the union by using the <tt> append </tt> procedure we defined in section
2-2-1:

<div id="">
(define (make-code-tree left right)
  (list left
        right
        (append (symbols left) (symbols right))
        (+ (weight left) (weight right))))
</div>
<script>
prompt();
</script>

If we make a tree in this way, we have the following selectors:

<div id="">
(define (left-branch tree) (car tree))

(define (right-branch tree) (cadr tree))

(define (symbols tree)
  (if (leaf? tree)
      (list (symbol-leaf tree))
      (caddr tree)))

(define (weight tree)
  (if (leaf? tree)
      (weight-leaf tree)
      (cadddr tree)))
</div>
<script>
prompt();
</script>

The procedures <tt> symbols </tt> and <tt> weight </tt> must do something slightly
different depending on whether they are called with a leaf or a general tree.
These are simple examples of <tt> generic procedures </tt> 
 (procedures that can
handle more than one kind of data), which we will have much more to say about
in sections 2-4 and 2-5.

<h4> The decoding procedure </h4> 

The following procedure implements the decoding algorithm.  It takes as
arguments a list of zeros and ones, together with a Huffman tree.

<div id="">
(define (decode bits tree)
  (define (decode-1 bits current-branch)
    (if (null? bits)
        '()
        (let ((next-branch
               (choose-branch (car bits) current-branch)))
          (if (leaf? next-branch)
              (cons (symbol-leaf next-branch)
                    (decode-1 (cdr bits) tree))
              (decode-1 (cdr bits) next-branch)))))
  (decode-1 bits tree))

(define (choose-branch bit branch)
  (cond ((= bit 0) (left-branch branch))
        ((= bit 1) (right-branch branch))
        (else (error "bad bit -- CHOOSE-BRANCH" bit))))
</div>
<script>
prompt();
</script>

The procedure <tt> decode-1 </tt> takes two arguments: the list of remaining bits
and the current position in the tree.  It keeps moving ``down'' the tree,
choosing a left or a right branch according to whether the next bit in the list
is a zero or a one.  (This is done with the procedure <tt> choose-branch </tt>.)
When it reaches a leaf, it returns the symbol at that leaf as the next symbol
in the message by <tt> cons </tt>ing it onto the result of decoding the rest of the
message, starting at the root of the tree.  Note the error check in the final
clause of <tt> choose-branch </tt>, which complains if the procedure finds
something other than a zero or a one in the input data.

<h4> Sets of weighted elements </h4> 

In our representation of trees, each non-leaf node contains a set of symbols,
which we have represented as a simple list.  However, the tree-generating
algorithm discussed above requires that we also work with sets of leaves and
trees, successively merging the two smallest items.  Since we will be required
to repeatedly find the smallest item in a set, it is convenient to use an
ordered representation for this kind of set.

We will represent a set of leaves and trees as a list of elements, arranged in
increasing order of weight.  The following <tt> adjoin-set </tt> procedure for
constructing sets is similar to the one described in Exercise 2-61;
however, items are compared by their weights, and the element being added to
the set is never already in it.

<div id="">
(define (adjoin-set x set)
  (cond ((null? set) (list x))
        ((< (weight x) (weight (car set))) (cons x set))
        (else (cons (car set)
                    (adjoin-set x (cdr set))))))
</div>
<script>
prompt();
</script>

The following procedure takes a list of symbol-frequency pairs such as
<tt> ((A 4) (B 2) (C 1) (D 1)) </tt> and constructs an initial ordered set of
leaves, ready to be merged according to the Huffman algorithm:

<div id="">
(define (make-leaf-set pairs)
  (if (null? pairs)
      '()
      (let ((pair (car pairs)))
        (adjoin-set (make-leaf (car pair)    ; symbol
                               (cadr pair))  ; frequency
                    (make-leaf-set (cdr pairs))))))
</div>
<script>
prompt();
</script>

<div class="exercise">
<b>Exercise 2.67:</b> Define an encoding tree and a
sample message:

<div id="">
(define sample-tree
  (make-code-tree (make-leaf 'A 4)
                  (make-code-tree
                   (make-leaf 'B 2)
                   (make-code-tree (make-leaf 'D 1)
                                   (make-leaf 'C 1)))))

(define sample-message '(0 1 1 0 0 1 0 1 0 1 1 1 0))
</div>
<script>
prompt();
</script>

Use the <tt> decode </tt> procedure to decode the message, and give the result.
</div>

<div class="exercise">
<b>Exercise 2.68:</b> The <tt> encode </tt> procedure takes
as arguments a message and a tree and produces the list of bits that gives the
encoded message.

<div id="">
(define (encode message tree)
  (if (null? message)
      '()
      (append (encode-symbol (car message) tree)
              (encode (cdr message) tree))))
</div>
<script>
prompt();
</script>

<tt> Encode-symbol </tt> is a procedure, which you must write, that returns the
list of bits that encodes a given symbol according to a given tree.  You should
design <tt> encode-symbol </tt> so that it signals an error if the symbol is not in
the tree at all.  Test your procedure by encoding the result you obtained in
Exercise 2-67 with the sample tree and seeing whether it is the same as
the original sample message.
</div>

<div class="exercise">
<b>Exercise 2.69:</b> The following procedure takes as
its argument a list of symbol-frequency pairs (where no symbol appears in more
than one pair) and generates a Huffman encoding tree according to the Huffman
algorithm.

<div id="">
(define (generate-huffman-tree pairs)
  (successive-merge (make-leaf-set pairs)))
</div>
<script>
prompt();
</script>

<tt> Make-leaf-set </tt> is the procedure given above that transforms the list of
pairs into an ordered set of leaves.  <tt> Successive-merge </tt> is the procedure
you must write, using <tt> make-code-tree </tt> to successively merge the
smallest-weight elements of the set until there is only one element left, which
is the desired Huffman tree.  (This procedure is slightly tricky, but not
really complicated.  If you find yourself designing a complex procedure, then
you are almost certainly doing something wrong.  You can take significant
advantage of the fact that we are using an ordered set representation.)
</div>

<div class="exercise">
<b>Exercise 2.70:</b> The following eight-symbol
alphabet with associated relative frequencies was designed to efficiently
encode the lyrics of 1950s rock songs.  (Note that the ``symbols'' of an
``alphabet'' need not be individual letters.)

<pre>
A     2 NA   16
BOOM  1 SHA  3
GET   2 YIP  9
JOB   2 WAH  1
</pre>

Use <tt> generate-huffman-tree </tt> (Exercise 2-69) to generate a
corresponding Huffman tree, and use <tt> encode </tt> (Exercise 2-68) to
encode the following message:

<pre>
Get a job

Sha na na na na na na na na

Get a job

Sha na na na na na na na na

Wah yip yip yip yip yip yip yip yip yip

Sha boom
</pre>

How many bits are required for the encoding?  What is the smallest number of
bits that would be needed to encode this song if we used a fixed-length code
for the eight-symbol alphabet?
</div>

<div class="exercise">
<b>Exercise 2.71:</b> Suppose we have a Huffman tree
for an alphabet of n symbols, and that the relative frequencies of the
symbols are 1, 2, 4, ..., 2^(n-1).  Sketch the tree for n=5; for
n=10.  In such a tree (for general n) how may bits are required to
encode the most frequent symbol?  the least frequent symbol?
</div>

<div class="exercise">
<b>Exercise 2.72:</b> Consider the encoding procedure
that you designed in Exercise 2-68.  What is the order of growth in the
number of steps needed to encode a symbol?  Be sure to include the number of
steps needed to search the symbol list at each node encountered.  To answer
this question in general is difficult.  Consider the special case where the
relative frequencies of the n symbols are as described in Exercise
2-71, and give the order of growth (as a function of n) of the number of
steps needed to encode the most frequent and least frequent symbols in the
alphabet.
</div>
@@

{{footnotes}}
@@
